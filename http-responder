#!/usr/bin/env perl
use Mojolicious::Lite;
use Time::HiRes qw(usleep);
use Mojo::Headers;
use Data::Dump qw/dump/;
use Mojo::Util qw/html_unescape/;

my $stash = {
    headers => 'Cache-Control: max-age=1, no-cache',
    body => '<h1>something</h1>',
    sleep => 100,
    status => 200,
};
any '/conf' => sub {
    my $self = shift;
    if ($self->req->method eq 'POST') {
        $stash->{headers} = $self->param('headers');
        $stash->{body} = html_unescape($self->param('body'));
        $stash->{sleep} = $self->param('sleep');
        $stash->{status} = $self->param('status');
    }
    $self->stash(%$stash, url => $self->url_for('/')->to_abs);
    $self->render('conf');
};

my $default =  sub {
  my $self = shift;
  $self->stash(%$stash);
  my $head_text = $stash->{headers};
  if ($head_text) {
      foreach my $l ( split(/\r\n/, $head_text ) ) {
          my ($key, $val) = (split(/:\s/, $l, 2));
          $self->res->headers->add($key, $val);
      }
  }
  warn "sleeping: " . $stash->{sleep};
  usleep($stash->{sleep} * 1000) if $stash->{sleep};
  warn "slept: " . $stash->{sleep};
  $self->res->code($stash->{status}) if $stash->{status};
  $self->render('index');
};

get '/' => $default;
get '/*rest' => $default;


app->start;
__DATA__
@@ index.html.ep

<%== $body %>

@@ conf.html.ep
% layout 'default';
% title 'http-responder';
<form method="POST">
<label>Headers</label>
<textarea name="headers"><%= $headers %></textarea>
<label>Body</label>
<textarea name="body"><%= $body %></textarea>
<label>Sleepytime (ms)</label>
<input type="number" name="sleep" value="<%= $sleep %>">
<label>status code</label>
<input type="number" name="status" value="<%= $status %>">
<button type="submit">Update</button>
<label>URL</label>
<input type="text" value="<%= $url %>" readonly>
<iframe src="<%= $url %>"></iframe>
@@ layouts/default.html.ep
<!DOCTYPE html>
<html>
  <head><title><%= title %></title>
  <style>
  label, textarea, input, button, iframe {
    display: block;
    min-width: 400px;
    margin-bottom: 10px;
  }
  </style>
  </head>
  <body><%= content %></body>
</html>
